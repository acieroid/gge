# inspired from http://www.scons.org/wiki/UsingPkgConfig
# vim: ft=python

env = Environment()

def CheckPKGConfig(context, version):
     context.Message( 'Checking for pkg-config... ' )
     ret = context.TryAction('pkg-config --atleast-pkgconfig-version=%s' % version)[0]
     context.Result( ret )
     return ret

def CheckPKG(context, name):
     context.Message( 'Checking for %s... ' % name )
     ret = context.TryAction('pkg-config --exists \'%s\'' % name)[0]
     context.Result( ret )
     return ret

# Configuration:
libsUsed = ['guile-1.8', 'sdl']

conf = Configure(env, custom_tests = { 'CheckPKGConfig' : CheckPKGConfig,
                                       'CheckPKG' : CheckPKG })

if not conf.CheckPKGConfig('0.15.0'):
     print 'pkg-config >= 0.15.0 not found.'
     Exit(1)

for lib in libsUsed:
  if not conf.CheckPKG(lib):
       print lib, 'not found.'
       Exit(1)

env = conf.Finish()

# Now, build:

libsStr = ''
for lib in libsUsed:
  libsStr += ' ' + lib
env.ParseConfig('pkg-config --cflags --libs' + libsStr)

env.Append(CCFLAGS = ['-O2'])
if int(ARGUMENTS.get('debug', 0)):
	env.Append(CCFLAGS = ['-g'])

# boost not in pkg-config
env.Append(LIBS=['boost_program_options', 'SDL_image'])

env.Program(target='gge',
            source = ['main.cpp',
                      'ScriptManager.cpp',
                      'GGEElement.cpp',  
                      'System.cpp',
                      'LogManager.cpp',
                      'Display.cpp'])
